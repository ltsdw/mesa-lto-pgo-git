##############################################################################

PGO="generate" # possible values are ("generate", "use", "off")
_march="native" # processor architeture, leave empty if unkown

# adapt the naming depeding on the compiled type
if [ "$PGO" != "off" ]; then
    pkgname="mesa-lto-pgo-$PGO-git"
    pkgdesc="an open-source implementation of the OpenGL specification, compiled with LTO and PGO. Git version"
else
    pkgname=mesa-lto-git
    pkgdesc="an open-source implementation of the OpenGL specification, compiled with LTO. Git version"
fi

##############################################################################

# sourcing scripts and envars
source "$PWD/scripts/utils/envar"
source "$PWD/scripts/aux-build"
source "$PWD/scripts/aux-package"
source "$PWD/scripts/aux-cleaning"
source "$PWD/scripts/utils/directories"

###############################################################################

_mesa_commit="#commit=f376597d"

pkgver=21.3.0_devel.143150.f376597d00d

pkgrel=1

arch=('x86_64')

makedepends=('git' 'python-mako' 'xorgproto' 'libxml2' 'libx11' 'elfutils' 'libxrandr' 'llvm' 'clang' 'meson' 'ninja' 'glslang')

depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf' 'libglvnd' 'libclc' 'zstd' 'expat' 'llvm-libs')

optdepends=('opengl-man-pages: for the OpenGL API man pages')

provides=('mesa' 'vulkan-intel' 'vulkan-driver' 'opengl-driver')

conflicts=('mesa' 'vulkan-intel')

url="https://www.mesa3d.org"

license=('custom')

source=("mesa::git+https://gitlab.freedesktop.org/mesa/mesa.git$_mesa_commit"
        "LICENSE"
       )

md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a'
        )

sha512sums=('SKIP'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2'
           )

##############################################################################

pkgver()
{
    cd mesa
    read -r _ver <VERSION
    echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare()
{
    # remove old _build directory if it exists
    _remove_build_dir

    # remove generated files from last build with PGO="generate" option
    _remove_pgo_build_dir
}

build()
{
    if [ "$PGO" = "generate" ]; then
        _generate_pgo_build
    elif [ "$PGO" = "use" ]; then
        _use_pgo_build
    else
        _no_pgo_build
    fi
}

package()
{
    # ninja install command, changed depending on type build
    _ninja_install

    if [ "$PGO" = "generate" ]; then
        # remove all unneeded files and directories from last pgo generate compile
        _remove_unneeded_pgo_build
    fi

    _remove_unneeded_files

    # indirect rendering
    ln -s "/usr/lib/libGLX_mesa.so.0" "$pkgdir/usr/lib/libGLX_indirect.so.0"

    install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
##############################################################################
