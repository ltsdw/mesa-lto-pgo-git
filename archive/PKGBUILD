##############################################################################

_pkgbuild_dir=$PWD

_mesa_commit="#commit=ab72be6c"

PGO="off" # possible values are ("generate", "use", "off")
_march="tigerlake" # processor architeture, leave empty if unkown

# adapt the naming depeding on the compiled type
if [ "$PGO" != "off" ]; then
    pkgdesc="an open-source implementation of the OpenGL specification, compiled with LTO and PGO. Git version"
else
    pkgdesc="an open-source implementation of the OpenGL specification, compiled with LTO. Git version"
fi

##############################################################################

# sourcing scripts and envars
source "$_pkgbuild_dir/scripts/utils/envar"
source "$_pkgbuild_dir/scripts/aux-build"
source "$_pkgbuild_dir/scripts/aux-package"
source "$_pkgbuild_dir/scripts/aux-cleaning"
source "$_pkgbuild_dir/scripts/utils/directories"

###############################################################################

pkgname=mesa-lto-pgo-git

pkgver=24.3.0_devel.ab72be6c5e9_off

pkgrel=1

arch=('x86_64')

makedepends=('git' 'python-mako' 'xorgproto' 'libxml2' 'elfutils' 'libxrandr'
             'llvm' 'clang' 'meson' 'ninja' 'glslang' 'python-ply' 'cbindgen' 'cmake' 'python-packaging' 'python-pyaml'
             'spirv-tools' 'spirv-llvm-translator'
             'wayland-protocols')

depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf' 'libglvnd' 'libclc'
         'libx11' 'zstd' 'expat' 'llvm-libs' 'wayland')

optdepends=('opengl-man-pages: for the OpenGL API man pages')

provides=('mesa' 'vulkan-intel' 'vulkan-driver' 'opengl-driver')

conflicts=('mesa' 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-swrast')

url="https://www.mesa3d.org"

license=('custom')

source=("mesa::git+https://gitlab.freedesktop.org/mesa/mesa.git$_mesa_commit"
        "LICENSE"
       )

sha256sums=('SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2'
           )

options=(!debug)

##############################################################################

pkgver()
{
    cd mesa
    read -r _ver <VERSION
    echo ${_ver/-/_}.$(git rev-parse --short HEAD)_$PGO
}

prepare()
{
    # remove old _build directory if it exists
    _remove_build_dir

    # remove generated files from last build with PGO="generate" option
    _remove_pgo_build_dir
}

build()
{
    if [ "$PGO" = "generate" ]; then
        _generate_pgo_build
    elif [ "$PGO" = "use" ]; then
        _use_pgo_build
    else
        _no_pgo_build
    fi
}

package()
{
    # ninja install command, changed depending on type build
    _ninja_install

    if [ "$PGO" = "generate" ]; then
        # remove all unneeded files and directories from last pgo generate compile
        _remove_unneeded_pgo_build
    fi

    _remove_unneeded_files

    # indirect rendering
    ln -s "/usr/lib/libGLX_mesa.so.0" "$pkgdir/usr/lib/libGLX_indirect.so.0"

    install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
##############################################################################
